> **"회전량을 전기적 신호로 측정할 수 있는 센서를 포함한 모터"**

- DC모터, 스테핑 모터, 서보모터 등 다양한 모터에 부착될 수 있다.
- **로봇, CNC, 3D 프린터, 드론, 전동차량** 등 정밀 제어가 필요한 곳에 많이 사용된다.

일반적으로 DC모터에 달려있는 엔코더는 홀센서엔코더이다.

홀센서란 소위 자기 센서로 자기장의 세기를 감지하는 센서이다. 자석과 홀센서 2개를 아래와 같이 배열하면 홀센서 엔코더가 된다.

![[Pasted image 20250520144700.png]]

홀센서의 출력은 위 그림의 우측과 같이 2개의 사각파가 1/4 주기 간격을 둔 형태가 된다. 모터가 정회전을 하면 A가 먼저 뛰고 B가 그 뒤에 뛰며, A가 가라앉고 그 뒤에 B가 가라앉게 되며 역회전을 하면 반대로 B가 먼저 뛰고 A가 그 뒤에 뛰며, B가 가라낮고 그 뒤에 A가 가라앉게 된다.

모터의 속도는 전압을 낮추는 것으로 해결할 수 있겠으나, 속도를 측정하거나 거리를 측정하는 역할은 엔코더 모터가 유일하다. 

| 항목       | 전압만 조절  | 엔코더 기반 제어          |
| -------- | ------- | ------------------ |
| 속도 조절    | 가능      | 가능 + 피드백 보정        |
| 거리 조절    | 불가      | 가능 (펄스 누적)         |
| 방향 전환    | 정확하지 않음 | 정확한 회전각 계산 가능      |
| 정밀도      | 낮음      | 높음                 |
| 로봇 위치 제어 | 어렵다     | 가능하다 (Odometry 기반) |

### 1. 증분형 엔코더
---
그림 1과 같이 디스크에 홈이 균일하게 있고, 발광부와 수광부가 있다. 발광부에서 빛을 내보내면 디스크가 회전함에 따라 홈이 파져 있으면 빛이 통과하여 수광부에서 빛을 검출하고, 홈이 파져 있지 않은 부분은 수광부에서 빛을 검출하지 못 한다. 디스크의 홈이 조밀하게 새겨져 있으면 정밀한 각도를 측정할 수 있겠다.

![[Pasted image 20250520145854.png]]

빛을 검출하느냐 못하느냐에 따라 그림 2와 같은 파형이 검출된다. 또한 1개의 라인에서만 홈을 관측하는 것이 아니라 2개의 라인에서 홈을 관측하여 정방향과 역방향까지 검출할 수 있는 것이 증분형 엔코더의 원리이다.

![[Pasted image 20250520145925.png]]

증분형 엔코더를 통해 펄스를 카운팅하는 방법에 대해 소개하도록 하겠다. A의 펄스만을 가지고 카운팅하여 각도를 측정할 수 있지만 정방향으로 회전하는지 역방향으로 회전하는지 알 수 없다. 

따라서 A와 B의 상태를 모두 확인하여 판단해야 한다. B 라인의 홈은 A 라인의 홈보다 90도 위상 차이를 가지고 있다.

![[Pasted image 20250520150003.png]]

먼저 그림 3과 같이 A 의상태가 변화할 때, 즉 LOW에서 HIGH로 변하거나 HIGH에서 LOW로 변하는 순간을 검출한다. 이를 Change interrupt라 하겠습니다. Change interrupt가 발생했을 때 정방향일 때는 +1을 카운트해주고 역방향인 경우는 -1을 카운트해야 한다. 정방향과 역방향의 판단은 B의 상태를 함께 보고 판단해야 한다.

정방향으로 회전할 때 change interrupt 상황이 발생하는 phase 2→3, 또는 4→1 을 살펴보겠다. 두 경우 A와 B가 모두 HIGH이거나 LOW이다. 

즉, A와 B의 상태가 같음을 확인할 수 있다. 반대로 역방향으로 회전할 때 change interrupt 상황이 발생하는 phase 1→4, 또는 3→2 를 살펴보겠다. 두 경우는 A와 B의 상태가 반대 상황이 됨을 확인할 수 있다. 정리하면, A 신호에서 change interrupt 가 발생할 때 A와 B의 상태가 일치하면 +1로 정방향으로 카운팅을 하고, A와 B의 상태가 반대라면 -1로 역방향으로 카운팅하면 되겠다. (표 1 참조)

![[Pasted image 20250520150044.png]]

Method 1에 비해 Method 2가 2배 더 정밀하게 측정할 수 있다는 장점이 있지만 인터럽트를 2개 모두 사용하고, 알고리즘이 약간 더 복잡하다는 측면이 니다.

참고로 로터리 엔코더의 resolution (분해능)은 CPR (count per revolution) 이라는 단위를 사용한다.Method 1 으로 하면 CPR의 2배, Method 2 로 하면 CPR의 4배에 해당하는 pulse를 측정할 수 있다. 즉, datasheet에 500 CPR이라 되어 있다면, Method 2 로 측정하면 한바퀴에 2000 pulse가 측정되는 것이다.

### 2. 제어 코드
---
``` cpp
#include <MsTimer2.h>  // 주기적으로 인터럽트를 발생시키기 위한 라이브러리

// 엔코더 A, B 채널 핀 설정
#define encoder_1_A 2
#define encoder_1_B 3

int estep_1 = 0;           // 엔코더 스텝 카운트
boolean direction_1;       // 회전 방향 저장 변수
int angle_1 = 0;           // 각도 (현재 사용되지 않음)

int motor_speed = 5;       // 모터 속도 제어용 PWM 출력 핀
int motor_dir_1 = 6;       // 모터 방향 제어 핀 1
int motor_dir_2 = 7;       // 모터 방향 제어 핀 2

int Ref_1 = 250;           // 목표 속도 (Pulse/s)
int Err_1 = 0;             // 현재 오차
int DErr_1 = 0;            // 오차 변화량 (Derivative)
int LErr_1 = 0;            // 이전 오차 (Last Error)

// PID 제어 계수
float kp = 0.2;            // 비례 계수
float ki = 0.2;            // 적분 계수
float kd = 0.03;           // 미분 계수

// PID 계산 결과 저장 변수
float result_P = 0;
float result_I = 0;
float result_D = 0;
float result = 0;

float vel_1;               // 현재 속도
float Lstep_1;             // 이전 스텝 값

void setup() {
  Serial.begin(9600);  // 시리얼 통신 시작 (모니터 출력용)

  // 핀 모드 설정
  pinMode(encoder_1_A, INPUT);
  pinMode(encoder_1_B, INPUT);
  pinMode(motor_speed, OUTPUT);
  pinMode(motor_dir_1, OUTPUT);
  pinMode(motor_dir_2, OUTPUT);

  // 엔코더 인터럽트 설정 (FALLING 엣지에서 encoder_1 함수 호출)
  attachInterrupt(0, encoder_1, FALLING);

  // 200ms마다 t_interrupt 함수 실행 (PID 제어 주기)
  MsTimer2::set(200, t_interrupt);
  MsTimer2::start();
}

void loop() {
  // 메인 루프에서는 아무것도 하지 않음 (PID는 인터럽트로 수행됨)
}

// 엔코더 인터럽트 함수: 회전 방향에 따라 스텝 값 증가/감소
void encoder_1() {
  direction_1 = digitalRead(encoder_1_B);  // B 채널 값 읽기

  if (direction_1 == HIGH) {
    estep_1--;  // 반시계 방향
  } else if (direction_1 == LOW) {
    estep_1++;  // 시계 방향
  }
}

// PID 제어 함수
void pid_control() {
  // 속도 계산: 일정 시간 동안의 엔코더 변화량
  vel_1 = (estep_1 - Lstep_1) / 0.2;
  Lstep_1 = estep_1;  // 현재 스텝 값을 다음 비교를 위해 저장

  // 오차 계산
  Err_1 = Ref_1 - vel_1;

  // PID 계산
  result_P = kp * Err_1;         // 비례항
  result_I += ki * Err_1;        // 적분항 (누적)
  DErr_1 = Err_1 - LErr_1;       // 오차 변화량
  result_D = kd * DErr_1;        // 미분항
  LErr_1 = Err_1;                // 현재 오차를 이전 오차로 저장

  result = result_P + result_I + result_D;  // PID 출력 합산

  // 모터 회전 방향 및 PWM 출력
  if (result >= 0) {
    digitalWrite(motor_dir_1, LOW);   // 정방향
    digitalWrite(motor_dir_2, HIGH);
    if (result > 255) result = 255;   // PWM 상한 제한
    analogWrite(motor_speed, result); // PWM 출력
  } else {
    digitalWrite(motor_dir_1, HIGH);  // 역방향
    digitalWrite(motor_dir_2, LOW);
    if (result < -255) result = -255; // PWM 하한 제한
    analogWrite(motor_speed, -result); // 음수이므로 부호 반전
  }
}

// 200ms마다 호출되는 타이머 인터럽트 함수
void t_interrupt() {
  pid_control();  // PID 제어 수행

  // 시리얼 모니터 출력
  Serial.print("PWM : ");
  Serial.print(result);
  Serial.print(", Velocity : ");
  Serial.print(vel_1);
  Serial.print(", Encoder_1 : ");
  Serial.println(estep_1);
}
```


### 3. 감속비
---
``` 
COUNTS_PER_DEGREE = (엔코더 CPR × 4 × 감속비) ÷ 360.0
```


